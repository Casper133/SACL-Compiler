/* Grammar */

source_code ::= (WHITE_SPACE_CHARACTER* LINE_BREAK_CHARACTER)* constants_block?
    (WHITE_SPACE_CHARACTER* LINE_BREAK_CHARACTER)+ config_block_body?
    (WHITE_SPACE_CHARACTER* LINE_BREAK_CHARACTER)*

constants_block ::= constant_declaration
    ((WHITE_SPACE_CHARACTER* LINE_BREAK_CHARACTER)+ constant_declaration)*

constant_declaration ::= CONST_KEYWORD WHITE_SPACE_CHARACTER+ record_declaration

config_block_body ::= (config_block | name_value_pair)
    ((WHITE_SPACE_CHARACTER* LINE_BREAK_CHARACTER)+ (config_block | name_value_pair))*

config_block ::= WHITE_SPACE_CHARACTER* identifier WHITE_SPACE_CHARACTER*
    LEFT_BRACE WHITE_SPACE_CHARACTER*
    (WHITE_SPACE_CHARACTER* LINE_BREAK_CHARACTER)+ config_block_body
    (WHITE_SPACE_CHARACTER* LINE_BREAK_CHARACTER)+ WHITE_SPACE_CHARACTER*
    RIGHT_BRACE WHITE_SPACE_CHARACTER*

name_value_pair ::= WHITE_SPACE_CHARACTER* record_declaration

record_declaration ::= identifier WHITE_SPACE_CHARACTER* EQUALS WHITE_SPACE_CHARACTER* record_value

identifier ::= TEXT_CHARACTER+

record_value ::= constant_call | escaped_sequence? characters_sequence

constant_call ::= DOLLAR_SIGN identifier

escaped_sequence ::= (BACKSLASH (BACKSLASH | DOLLAR_SIGN))+

characters_sequence ::= (TEXT_CHARACTER | WHITE_SPACE_CHARACTER)+

CONST_KEYWORD ::=
    (TEXT_CHARACTER - [#x21-#x62 #x64-#xFF])               /* c */
    (TEXT_CHARACTER - [#x21-#x6E #x70-#xFF])               /* o */
    (TEXT_CHARACTER - [#x21-#x6D #x6F-#xFF])               /* n */
    (TEXT_CHARACTER - [#x21-#x72 #x74-#xFF])               /* s */
    (TEXT_CHARACTER - [#x21-#x73 #x75-#xFF])               /* t */

LEFT_BRACE ::= TEXT_CHARACTER - [#x21-#x7A #x7C-#xFF]      /* { */

RIGHT_BRACE ::= TEXT_CHARACTER - [#x21-#x7C #x7E-#xFF]     /* } */

EQUALS ::= TEXT_CHARACTER - [#x21-#x3C #x3E-#xFF]          /* = */

DOLLAR_SIGN ::= TEXT_CHARACTER - [#x21-#x23 #x25-#xFF]     /* $ */

BACKSLASH ::= TEXT_CHARACTER - [#x21-#x5B #x5D-#xFF]       /* \ */


/* Tokens */

WHITE_SPACE_CHARACTER ::= #x09 | #x20         /* Tab | Space */

LINE_BREAK_CHARACTER ::= #x0D #x0A? | #x0A    /* CR LF | CR | LF */

TEXT_CHARACTER ::= [#x21-#xFF]                /* Extended ASCII table without control characters */
