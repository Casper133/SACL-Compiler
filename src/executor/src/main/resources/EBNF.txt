/* Grammar */

source_code ::= (WHITE_SPACE_CHARACTER* LINE_BREAK_CHARACTER)* constants_block?
    (WHITE_SPACE_CHARACTER* LINE_BREAK_CHARACTER)+ config_block_body?
    (WHITE_SPACE_CHARACTER* LINE_BREAK_CHARACTER)*

constants_block ::= constant_declaration
    ((WHITE_SPACE_CHARACTER* LINE_BREAK_CHARACTER)+ constant_declaration)*

constant_declaration ::= C_LETTER O_LETTER N_LETTER S_LETTER T_LETTER WHITE_SPACE_CHARACTER+ record_declaration

config_block_body ::= (config_block | name_value_pair)
    ((WHITE_SPACE_CHARACTER* LINE_BREAK_CHARACTER)+ (config_block | name_value_pair))*

config_block ::= WHITE_SPACE_CHARACTER* identifier WHITE_SPACE_CHARACTER*
    LEFT_BRACE WHITE_SPACE_CHARACTER*
    (WHITE_SPACE_CHARACTER* LINE_BREAK_CHARACTER)+ config_block_body
    (WHITE_SPACE_CHARACTER* LINE_BREAK_CHARACTER)+ WHITE_SPACE_CHARACTER*
    RIGHT_BRACE WHITE_SPACE_CHARACTER*

name_value_pair ::= WHITE_SPACE_CHARACTER* record_declaration

record_declaration ::= identifier WHITE_SPACE_CHARACTER* EQUALS WHITE_SPACE_CHARACTER* record_value

identifier ::=
    (TEXT_CHARACTER
      | C_LETTER
      | O_LETTER
      | N_LETTER
      | S_LETTER
      | T_LETTER
      | LEFT_BRACE
      | RIGHT_BRACE
      | EQUALS
      | DOLLAR_SIGN
      | BACKSLASH)+

record_value ::= constant_call | escaped_sequence? characters_sequence

constant_call ::= DOLLAR_SIGN identifier

escaped_sequence ::= (BACKSLASH (BACKSLASH | DOLLAR_SIGN))+

characters_sequence ::=
    (TEXT_CHARACTER
      | C_LETTER
      | O_LETTER
      | N_LETTER
      | S_LETTER
      | T_LETTER
      | LEFT_BRACE
      | RIGHT_BRACE
      | EQUALS
      | DOLLAR_SIGN
      | BACKSLASH
      | WHITE_SPACE_CHARACTER)+


/* Tokens */

WHITE_SPACE_CHARACTER ::= #x09 | #x20         /* Tab | Space */

LINE_BREAK_CHARACTER ::= #x0D #x0A? | #x0A    /* CR LF | CR | LF */

/*
 Extended ASCII table without control characters

 Skipped symbols:
  #x24 - $
  #x3D - =
  #x5C - \
  #x63 - c
  #x6E - n
  #x6F - o
  #x73 - s
  #x74 - t
  #x7B - {
  #x7D - }
 */
TEXT_CHARACTER ::= [#x21-#x23]
    | [#x25-#x3C]
    | [#x3E-#x5B]
    | [#x5D-#x62]
    | [#x64-#x6D]
    | [#x70-#x72]
    | [#x75-#x7A]
    | #x7C
    | [#x7E-#xFF]

C_LETTER ::= "c"

O_LETTER ::= "o"

N_LETTER ::= "n"

S_LETTER ::= "s"

T_LETTER ::= "t"

LEFT_BRACE ::= "{"

RIGHT_BRACE ::= "}"

EQUALS ::= "="

DOLLAR_SIGN ::= "$"

BACKSLASH ::= "\"
