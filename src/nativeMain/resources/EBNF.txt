source_code ::= END_OF_LINE* constants_block? END_OF_LINE+ config_block_body? END_OF_LINE*

constants_block ::= constant_declaration (LINES_SEPARATOR constant_declaration)*

constant_declaration ::= CONST_KEYWORD record_declaration

config_block_body ::= (config_block | name_value_pair) (LINES_SEPARATOR (config_block | name_value_pair))*

config_block ::= WHITE_SPACE_CHARACTER* identifier BEGIN_CONFIG_BLOCK_BODY config_block_body END_CONFIG_BLOCK_BODY

name_value_pair ::= WHITE_SPACE_CHARACTER* record_declaration

record_declaration ::= identifier NAME_VALUE_SEPARATOR record_value

record_value ::= constant_call | escaped_sequence characters_sequence | characters_sequence

constant_call ::= "$" identifier

identifier ::= ([A-Z] | [a-z] | [0-9] | "_" | "-" | ".")+

escaped_sequence ::= ("\" ("\" | "$"))+

characters_sequence ::= CHARACTER_SET+




LINES_SEPARATOR ::= END_OF_LINE+

CONST_KEYWORD ::= "const" WHITE_SPACE_CHARACTER+

BEGIN_CONFIG_BLOCK_BODY ::= WHITE_SPACE_CHARACTER* "{" WHITE_SPACE_CHARACTER* END_OF_LINE+

END_CONFIG_BLOCK_BODY ::= END_OF_LINE+ WHITE_SPACE_CHARACTER* "}" WHITE_SPACE_CHARACTER*

NAME_VALUE_SEPARATOR ::= WHITE_SPACE_CHARACTER* "=" WHITE_SPACE_CHARACTER*

CHARACTER_SET ::= [#x21-#xFF] | WHITE_SPACE_CHARACTER   /* Extended ASCII table without control characters */

END_OF_LINE ::= WHITE_SPACE_CHARACTER* LINE_BREAK_CHARACTER

WHITE_SPACE_CHARACTER ::= #x20 | #x09   /* Space | Tab */

LINE_BREAK_CHARACTER ::= (CARRIAGE_RETURN LINE_FEED)
    | CARRIAGE_RETURN
    | LINE_FEED

CARRIAGE_RETURN ::= #x0D        /* CR */

LINE_FEED ::= #x0A              /* LF */

